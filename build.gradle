
import cam72cam.universalmodcore.CurseForge;
import cam72cam.universalmodcore.Util;

buildscript {
	repositories {
		maven { url = "https://teamopenindustry.cc/maven" }
	}
	dependencies {
		classpath 'cam72cam.universalmodcore:UniversalModCoreGradle:0.1.3'
	}
}

plugins {
	id 'fabric-loom' version '0.2.6-SNAPSHOT'
	id 'maven-publish'
	id 'maven'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

String umcVersion = "1.0.1"
if (!"release".equalsIgnoreCase(System.getProperty("target"))) {
	umcVersion += "-" + Util.GitRevision()
}
version = "1.14.4-forge-" + umcVersion

archivesBaseName = project.archives_base_name
group = project.maven_group

minecraft {
}

repositories {
	maven {
		name = "BuildCraft"
		url = "https://mod-buildcraft.com/maven"
	}
	maven { url = "http://server.bbkr.space:8081/artifactory/libs-snapshot" }
	maven { url "https://maven.fabricmc.net/io/github/prospector/modmenu/ModMenu/" }
}


dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modCompile "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modCompile "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
	// You may need to force-disable transitiveness on them.

	compile group: 'javax.vecmath', name: 'vecmath', version: '1.5.2'
	include group: 'javax.vecmath', name: 'vecmath', version: '1.5.2'

	modImplementation "alexiil.mc.lib:libblockattributes-all:0.5.0"
	include "alexiil.mc.lib:libblockattributes-all:0.5.0"

	compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.0'
	include group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.0'

	modImplementation "io.github.cottonmc:cotton-energy:1.5.0+1.14.3-SNAPSHOT"
	include "io.github.cottonmc:cotton-energy:1.5.0+1.14.3-SNAPSHOT"

	testCompile('junit:junit:4.13')

	compile group: 'org.lz4', name: 'lz4-java', version: '1.7.1'
	include group: 'org.lz4', name: 'lz4-java', version: '1.7.1'
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}

test {
	useJUnit()
	maxHeapSize = '1G'
}

task deployJar(type: Jar)

configurations {
    deployerJars
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-ssh:2.9"
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        repository(url: "scp://teamopenindustry.cc/var/www/html/maven/") {
            authentication(userName: "mavenci", privateKey: "publish_key")
        }
    }
}
